# Maintainer: cpasjuste <cpasjuste@gmail.com>

pkgname=ps4-openorbis-musl
pkgver=1.4
pkgrel=2
pkgdesc='musl for ps4'
arch=('any')
url='https://github.com/OpenOrbis/musl'
license=('MIT')
options=(!strip libtool)
source=("https://github.com/OpenOrbis/musl/archive/5f3a45f5638c1a6c6b3445e54cd6a2f67fad4d48.zip")
sha256sums=('891f0a3cd8550a59e879f8ea799736a35062275e73ab7b858d84ccdc6d4e67ff')
depends=('ps4-openorbis-clang')
groups=('ps4-openorbis')

build() {
  cd musl-5f3a45f5638c1a6c6b3445e54cd6a2f67fad4d48
  
  ./configure --target=x86_64-scei-ps4 \
    --prefix=/opt/pacbrew/ps4/openorbis \
    --disable-shared \
    CC=/opt/pacbrew/ps4/openorbis/bin/clang \
    CFLAGS="-fPIC -DPS4 -D_LIBUNWIND_IS_BAREMETAL=1"

  make --quiet -j $(getconf _NPROCESSORS_ONLN)
}

package() {
  cd musl-5f3a45f5638c1a6c6b3445e54cd6a2f67fad4d48
  make DESTDIR="${pkgdir}" install
  
  # "ps4-openorbis-libcxx" package will merge compiler-rt into libc
  mv "${pkgdir}"/opt/pacbrew/ps4/openorbis/lib/libc.a "${pkgdir}"/opt/pacbrew/ps4/openorbis/lib/libcmusl.a
}
